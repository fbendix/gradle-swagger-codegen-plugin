buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.7"
    }
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: "com.gradle.plugin-publish"

def GradleSwaggerCodegenPluginGroupId = 'at.bendix.gradle'
def GradleSwaggerCodegenPluginVersion = '0.1.1'
def GradleSwaggerCodegenPluginArtifact = 'gradle-swagger-codegen-plugin'
def GradleSwaggerCodegenPluginUrl = 'https://github.com/fbix/gradle-swagger-codegen-plugin'

// In GitLab project variables are not allowed to contain dots
project.afterEvaluate {
    // Get values from $USER_HOME/.gradle/gradle.properties
    def key = project.properties['gradle_publish_key']
    def secret = project.properties['gradle_publish_secret']

    if (key && secret) {
        System.properties.setProperty('gradle.publish.key', key)
        System.properties.setProperty('gradle.publish.secret', secret)
    }
}

pluginBundle {
    website = GradleSwaggerCodegenPluginUrl
    vcsUrl = GradleSwaggerCodegenPluginUrl
    description = 'Gradle plugin for Swagger Codegen'
    tags = ['swagger-codegen', 'gradle']

    plugins {
        greetingsPlugin {
            id = 'com.fbix.gradle.swagger.codegen'
            displayName = 'Gradle plugin for Swagger Codegen'
        }
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from 'build/docs/groovydoc'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

publishing {
    repositories {
        maven {
            // change to point to your repo, e.g. http://my.org/repo
            url "$buildDir/repo"
        }
    }
    publications {
        GradleSwaggerCodegenPluginPublish(MavenPublication) {
            from components.java
            groupId GradleSwaggerCodegenPluginGroupId
            artifactId GradleSwaggerCodegenPluginArtifact
            version GradleSwaggerCodegenPluginVersion

            artifact sourcesJar
            artifact javadocJar
        }
    }
}



group GradleSwaggerCodegenPluginGroupId
version GradleSwaggerCodegenPluginVersion

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
    compile 'org.zeroturnaround:zt-zip:1.8'

    testCompile group: 'junit', name: 'junit', version: '4.11'
}
